plugins {
    id 'idea'
    id 'cz.alenkacz.gradle.scalafmt' version '1.8.0' apply false
    id 'com.linkedin.python' version '0.8.11' apply false
    id 'com.github.arekbee.gradle-R-plugin' version '0.12' apply false
    id 'com.github.johnrengelman.shadow' version '5.0.0' apply false
}

allprojects {
    repositories {
        jcenter()
        maven { url 'http://dl.bintray.com/spark-packages/maven' }
        maven { url 'http://repo.artima.com/releases' }
    }
}

ext {
    scalaVersion = '2.12.8'
    sparkDariaVersion = '0.31.0-s_2.12'
    sparkVersion = '2.4.3'
}

subprojects {
    if (name.startsWith('streaming')) {
        apply plugin: 'com.linkedin.python'
        apply plugin: 'java'
    }
    if (name.startsWith('transformation')) {
        apply plugin: 'scala'
        apply plugin: 'java'
        apply plugin: 'com.github.johnrengelman.shadow'
        apply plugin: 'cz.alenkacz.gradle.scalafmt'
    }
    if (name.startsWith('visualization')) {
        apply plugin: 'com.github.arekbee.gradle-R-plugin'
    }
}
